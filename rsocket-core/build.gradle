/*
 * Copyright 2016 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'io.morethan.jmhreport'

// disable tasks to stop generating duplicate files
jmhClasses.enabled = false
jmhRunBytecodeGenerator.enabled = false
jmhCompileGeneratedClasses.enabled = false

jmhJar {
    // add jmh classes to shadow jar
    from project.configurations.jmh
    from project.configurations.jmhRuntime

    // exclude logging classes
    exclude 'org.slf4j:slf4j-log4j12'
    exclude 'log4j:log4j'
}

jmh {
    jmhVersion = '1.19'
    includeTests = false
    duplicateClassesStrategy = DuplicatesStrategy.WARN
    zip64 = true

    jvmArgs = ['-XX:+UnlockCommercialFeatures', '-XX:+FlightRecorder']
    // NOTE: uncomment to add specific options
    // jvmArgsAppend = ['-XX:+UseG1GC', '-Xms4g', '-Xmx4g']
    profilers = ['gc']
    resultFormat = 'JSON'

    // include = ['io.rsocket.RSocketPerf.fireAndForgetHello']
}

// run report generation after benchmark
tasks.jmh.finalizedBy tasks.jmhReport

jmhReport {
    jmhResultPath = project.file('build/reports/jmh/results.json')
    jmhReportOutput = project.file('build/reports/jmh')
}

// remove directory generated by IDEA during benchmark
clean {
    project.delete('out')
}

dependencies {
    jmh "org.openjdk.jmh:jmh-core:${jmh.jmhVersion}"
    jmh "org.openjdk.jmh:jmh-generator-annprocess:${jmh.jmhVersion}"

    jmhRuntime "org.slf4j:slf4j-nop:$slf4jVersion"
}